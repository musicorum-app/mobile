import com.github.triplet.gradle.androidpublisher.ReleaseStatus

plugins {
    id 'com.android.application'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.10'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'dagger.hilt.android.plugin'
    id 'com.github.triplet.play' version '3.7.0'
    id 'com.google.devtools.ksp'
    id "io.sentry.android.gradle" version "3.12.0"
}

def appKeyStoreFile = "../upload_key.jks"
def appKeyStorePassword = System.env.KEY_STORE_PASSWORD
def appKeyAlias = System.env.KEY_ALIAS
def appKeyPassword = System.env.KEY_PASSWORD

sentry {
    includeProguardMapping = true
    autoUploadProguardMapping = true
    experimentalGuardsquareSupport = true
}

android {
    namespace 'io.musicorum.mobile'
    compileSdk 34

    signingConfigs {
        release {
            storeFile file(appKeyStoreFile)
            storePassword appKeyStorePassword
            keyAlias appKeyAlias
            keyPassword appKeyPassword
        }
    }

    lintOptions {
        disable 'NullSafeMutableLiveData'
    }


    defaultConfig {
        applicationId "io.musicorum.mobile"
        minSdk 28
        targetSdk 34
        versionCode 63
        versionName "1.18.3-pre-release"
        //compileSdkPreview = "UpsideDownCake"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        def tokensFile = rootProject.file("tokens.properties")
        def tokens = new Properties()
        tokens.load(new FileInputStream(tokensFile))
        buildConfigField("String", "LASTFM_API_KEY", tokens["LASTFM_API_KEY"])
        buildConfigField("String", "LASTFM_SECRET", tokens["LASTFM_SECRET"])
        buildConfigField("String", "MUSICORUM_API_KEY", tokens["MUSICORUM_API_KEY"])
    }

    buildTypes {
        release {
            def releaseTokensFile = rootProject.file("tokens.release.properties")
            def releaseTokens = new Properties()
            releaseTokens.load(new FileInputStream(releaseTokensFile))

            signingConfig signingConfigs.release
            buildConfigField("String", "LASTFM_API_KEY", releaseTokens["LASTFM_API_KEY"])
            buildConfigField("String", "LASTFM_SECRET", releaseTokens["LASTFM_SECRET"])
            buildConfigField("String", "MUSICORUM_API_KEY", releaseTokens["MUSICORUM_API_KEY"])
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel = "SYMBOL_TABLE"
            }
        }
        debug {
            versionNameSuffix '-debug'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.3'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

play {
    double fraction = 100
    def googleServiceAccountKeyFile = "../google_sa_key.json"
    track.set("internal")
    serviceAccountCredentials.set(file(googleServiceAccountKeyFile))
    releaseStatus.set(ReleaseStatus.COMPLETED)
    play.userFraction.set(fraction)
}

dependencies {
    implementation 'com.github.skydoves:balloon-compose:1.5.2'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'
    implementation "androidx.paging:paging-compose:3.3.0-alpha02"
    implementation 'com.github.crowdin.mobile-sdk-android:sdk:1.5.7'
    implementation platform('androidx.compose:compose-bom:2022.11.00')
    implementation 'com.google.android.play:app-update:2.1.0'
    implementation 'com.google.android.play:app-update-ktx:2.1.0'
    implementation 'org.jsoup:jsoup:1.15.3'

    implementation 'io.sentry:sentry-android:6.27.0'
    implementation 'io.sentry:sentry-compose-android:6.27.0'

    def room_version = "2.5.2"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    implementation "com.google.dagger:hilt-android:2.44.2"
    implementation 'androidx.core:core-ktx:1.10.0'
    kapt "com.google.dagger:hilt-compiler:2.48"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    implementation 'com.google.firebase:firebase-messaging-ktx:23.2.1'
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    implementation 'app.rive:rive-android:4.0.0'
    implementation "androidx.startup:startup-runtime:1.1.1"
    implementation "com.google.accompanist:accompanist-swiperefresh:0.27.0"
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation platform('com.google.firebase:firebase-bom:31.0.2')
    implementation 'androidx.browser:browser:1.6.0'
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-serialization-kotlinx-json:$ktor_version"
    implementation "io.ktor:ktor-client-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-serialization-kotlinx-xml:$ktor_version"
    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.26.5-rc'
    implementation 'androidx.navigation:navigation-compose:2.7.1'
    implementation "androidx.compose.material:material-icons-extended"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation "io.ktor:ktor-client-logging:$ktor_version"
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'com.github.ajalt.colormath:colormath:3.2.1'
    implementation 'com.github.ajalt.colormath.extensions:colormath-ext-jetpack-compose:3.2.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'


    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.8.0-rc01'
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation 'androidx.compose.material3:material3:1.2.0-alpha06'
    implementation 'com.google.accompanist:accompanist-placeholder-material:0.26.5-rc'
    implementation 'io.coil-kt:coil-compose:2.4.0'
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
}

kapt {
    correctErrorTypes true
}

hilt {
    enableAggregatingTask true
}
